external inner function val MulRateToBase1(var string,val,val,val,val,val,val,roundmode);
external inner procedure GetVATdouble(string,var val,var val,Integer);
external inner function Boolean FindLocalSerialPortDevice(Integer,LongInt,string,var record LSerialPortDeviceVc);

enum begin
  kAPSACommandOpen = 0,
  kAPSACommandVAT = 1,
  kAPSACommandClose = 2,
  kAPSACommandAddItem = 3,
  kAPSACommandPay = 4,
  kAPSACommandStatus = 5,
  kAPSACommandBeginReceipt = 6,
  kAPSACommandEndReceipt = 7,
  kAPSACommandComment = 8,
  kAPSACommandZReport = 9,
  kAPSACommandXReport = 10
end;

function string 255 GetAPSAUrl(record LSerialPortDeviceVc LSPDr)
begin
  string 255 res;

  res = LSPDr.Port;

  GetAPSAUrl = res;
  return;
end;

function Integer ExecAPSARequest(record LSerialPortDeviceVc LSPDr,string path,area request,var JSON js)
begin
  string 255 url,stat;
  area reply;
  Integer res;
  Longint tick;

  url = GetAPSAUrl(LSPDr);
  res = 12400;//problem with connection
  if (SendWebRequest(url,8111,-1,false,"POST",path,"application/json","",false,request,reply,30)) then begin
    js = ParseJSONArea(reply);
    if (right(path,4)=="open") then begin
      if (JSONGet(js,"OpenResult")=="000000" or len(JSONGet(js,"OpenResult"))!=6) then begin
        res = 19389;//failed to open session
      end else begin
        res = 0;
      end;
    end else begin
      if (right(path,5)=="close") then begin//should redo with a switch
        if (JSONGet(js,"CloseResult")=="OK") then begin
          res = 0;
        end else begin
          res = 19389;
        end;
      end else begin
        stat = JSONGet(js,"CmdlineResult");
        stat = left(stat,2);
        if (stat=="OK") then begin
          res = 0;
        end;
        if (stat=="ER") then begin
          res = 19381;//problem with reply. possibly get more info and return to user
        end;
        if (stat=="NO") then begin
          res = 12400;//no connection to printer
        end;
      end;
    end;
  end;
  tick = GetCurTick;
  writeareatofile(reply,"_apsa.reply." & tick & ".txt",0);
  writeareatofile(request,"_apsa.request" & tick & ".txt",0);

  ExecAPSARequest = res;
  return;
end;

function Integer SendAPSASimpleCommand(record LSerialPortDeviceVc LSPDr,Integer command)
begin
  Integer res;
  area request;
  string 255 path;
  JSON js;

  switch (command) begin
    case kAPSACommandOpen:
      path = "/json/fp550/Open";
      AddTextToArea("{""port"":""0"",""settings"":""0""}",request);
    case kAPSACommandClose:
      path = "/json/fp550/Close";
    case kAPSACommandBeginReceipt:
      path = "/json/fp550/Cmdline";
      AddTextToArea("{""cmd"":""48"",""parameter"":""""}",request);
    case kAPSACommandEndReceipt:
      path = "/json/fp550/Cmdline";
      AddTextToArea("{""cmd"":""56"",""parameter"":""""}",request);
    case kAPSACommandZReport:
      path = "/json/fp550/Cmdline";
      AddTextToArea("{""cmd"":""69"",""parameter"":""0""}",request);
    case kAPSACommandXReport:
      path = "/json/fp550/Cmdline";
      AddTextToArea("{""cmd"":""69"",""parameter"":""2""}",request);
  end;

  res = ExecAPSARequest(LSPDr,path,request,js);

  SendAPSASimpleCommand = res;
  return;
end;

function string 255 GetVATCode(Integer InclVAT,string vatcode,vector val vatrates)
begin
  string 255 res;
  val vatprc,tax1;

  GetVATdouble(vatcode,vatprc,tax1,0);

  if (vatprc==vatrates[0]) then begin 
    res = "A"; 
    goto LGetVATCode;
  end;
  if (vatprc==vatrates[1]) then begin 
    res = "B"; 
    goto LGetVATCode;
  end;
  if (vatprc==vatrates[2]) then begin 
    res = "C"; 
    goto LGetVATCode;
  end;
  if (vatprc==vatrates[3]) then begin 
    res = "D"; 
    goto LGetVATCode;
  end;
  if (vatprc==vatrates[4]) then begin 
    res = "E"; 
    goto LGetVATCode;
  end;
  if (vatprc==vatrates[5]) then begin 
    res = "F"; 
    goto LGetVATCode;
  end;
LGetVATCode:;
  GetVATCode = res;
  return;
end;

function Integer IVCash_PrintRecItem(record LSerialPortDeviceVc LSPDr,record IVCashVc IVCashr,row IVCashVc IVCashrw,Boolean voidf,vector val vatrates)
begin
  Integer res;
  area request;
  string 255 path,itemline,spec,vatcode;
  JSON js;

  spec = IVCashrw.Spec;
  if (nonblank(IVCashrw.SerialNr)) then begin
    spec = spec & "\\n" & IVCashrw.SerialNr;
  end;
  if (voidf) then begin
    IVCashrw.Quant = -IVCashrw.Quant;
  end;

  vatcode = GetVATCode(IVCashr.InclVAT,IVCashrw.VATCode,vatrates);

  itemline = spec & "\\t" & vatcode & IVCashrw.Price;
  if (IVCashrw.Quant!=1) then begin
    itemline = itemline & "*" & ValToString(IVCashrw.Quant,M40Val,"",".",0);
  end;
  
  if (nonblank(IVCashrw.vRebate)) then begin
    itemline = itemline & "," & IVCashrw.vRebate;
  end;
  itemline = ConvertStringToCodePage("CP1257",itemline);
  path = "/json/fp550/Cmdline";
  AddTextToArea("{""cmd"":""49"",""parameter"":""" & itemline & """}",request);

  res = ExecAPSARequest(LSPDr,path,request,js);

  IVCash_PrintRecItem = res;
  return;
end;

procedure IVCash_CalculatePaymentModeSums(record IVCashVc IVCashr,var vector val vpaymodesums)
begin
  row IVCashVc IVCashrw;
  Integer i,rwcnt;
  val t;
  
  vpaymodesums[kInvoiceRowTypeCreditCardPayment] = 0.00;
  vpaymodesums[kInvoiceRowTypeCashPayment] = 0.00;
  rwcnt = MatRowCnt(IVCashr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(IVCashr,i,IVCashrw);
    switch (IVCashrw.stp) begin
//      case kInvoiceRowTypeGiftVoucherPayment:
      case kInvoiceRowTypeCreditCardPayment:
        vpaymodesums[kInvoiceRowTypeCreditCardPayment] = vpaymodesums[kInvoiceRowTypeCreditCardPayment] + MulRateToBase1(IVCashrw.CurncyCode,IVCashrw.Sum,IVCashrw.FrRate,IVCashrw.ToRateB1,IVCashrw.ToRateB2,IVCashrw.BaseRate1,IVCashrw.BaseRate2,DefaultCurRoundOff);
//      case kInvoiceRowTypeLoyaltyPointsPayment:
      case kInvoiceRowTypeCashPayment:
        vpaymodesums[kInvoiceRowTypeCashPayment] = vpaymodesums[kInvoiceRowTypeCashPayment] + MulRateToBase1(IVCashrw.CurncyCode,IVCashrw.Sum,IVCashrw.FrRate,IVCashrw.ToRateB1,IVCashrw.ToRateB2,IVCashrw.BaseRate1,IVCashrw.BaseRate2,DefaultCurRoundOff);
//      case kInvoiceRowTypeChequePayment:
    end;
  end;
  t = IVCashr.BaseSum4 - vpaymodesums[kInvoiceRowTypeCashPayment] - vpaymodesums[kInvoiceRowTypeCreditCardPayment];
  if (t>0) then begin
    vpaymodesums[kInvoiceRowTypeCashPayment] = vpaymodesums[kInvoiceRowTypeCashPayment] + t;  
  end;
  return;
end;

function Integer SendAPSAPayments(record LSerialPortDeviceVc LSPDr,record IVCashVc IVCashr)
begin
  Integer res;
  area request;
  string 255 path;
  vector val vpaymodesums;
  JSON js;

  //payments = GetPayments;

  IVCash_CalculatePaymentModeSums(IVCashr,vpaymodesums);
  
  path = "/json/fp550/Cmdline";
  if (vpaymodesums[kInvoiceRowTypeCreditCardPayment]!=0) then begin
    AddTextToArea("{""cmd"":""53"",""parameter"":""\\tC" & ValToString(vpaymodesums[kInvoiceRowTypeCreditCardPayment],M4Val,"",".",0) & """}",request);
    res = ExecAPSARequest(LSPDr,path,request,js);
  end;

  SetAreaZeroSize(request);
  if (vpaymodesums[kInvoiceRowTypeCashPayment]!=0) then begin
    AddTextToArea("{""cmd"":""53"",""parameter"":""\\tP" & ValToString(vpaymodesums[kInvoiceRowTypeCashPayment],M4Val,"",".",0) & """}",request);
    res = ExecAPSARequest(LSPDr,path,request,js);
  end;

  SendAPSAPayments = res;
  return;
end;


function Integer BeginFiscalReceipt(record LSerialPortDeviceVc LSPDr)
begin
  Integer res;
  
  res = SendAPSASimpleCommand(LSPDr,kAPSACommandBeginReceipt);

  BeginFiscalReceipt = res;
  return;
end;

function Integer APSAOpen(record LSerialPortDeviceVc LSPDr)
begin
  Integer res;
  
  res = SendAPSASimpleCommand(LSPDr,kAPSACommandOpen);

  APSAOpen = res;
  return;
end;

function Integer APSAClose(record LSerialPortDeviceVc LSPDr)
begin
  Integer res;
  
  res = SendAPSASimpleCommand(LSPDr,kAPSACommandClose);

  APSAClose = res;
  return;
end;

function Integer IVCash_PrintCommentLine(record LSerialPortDeviceVc LSPDr,string comment)
begin
  Integer res;
  string 255 path;
  area request;
  JSON js;

  path = "/json/fp550/Cmdline";
  AddTextToArea("{""cmd"":""54"",""parameter"":""" & ConvertStringToCodePage("CP1257",comment) & """}",request);

  res = ExecAPSARequest(LSPDr,path,request,js);

  IVCash_PrintCommentLine = res;
  return;
end;


function Integer IVCash_EndFiscalReceipt(record LSerialPortDeviceVc LSPDr)
begin
  Integer res;

  res = SendAPSASimpleCommand(LSPDr,kAPSACommandEndReceipt);

  IVCash_EndFiscalReceipt = res;
  return;
end;


function Integer IVCash_PrintItemRows(record LSerialPortDeviceVc LSPDr,record IVCashVc IVCashr,vector val vatrates)
begin
  Integer res,noErr;
  row IVCashVc IVCashrw;
  Integer i,rwcnt;

  rwcnt = MatRowCnt(IVCashr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(IVCashr,i,IVCashrw);
    switch (IVCashrw.stp) begin
      case kInvoiceRowTypeStructuredItemComponent:
        goto LkInvoiceRowTypeNormal2;
      case kInvoiceRowTypeNormal:
LkInvoiceRowTypeNormal2:;
        if (nonblank(IVCashrw.ArtCode)) then begin
          res = IVCash_PrintRecItem(LSPDr,IVCashr,IVCashrw,false,vatrates);
          if (res!=noErr) then begin
            goto LPrintItemRows;
          end;
        end else begin
          res = IVCash_PrintCommentLine(LSPDr,IVCashrw.Spec);
          if (res!=noErr) then begin
            goto LPrintItemRows;
          end;
        end;
      case kInvoiceRowTypeVoid:
        res = IVCash_PrintRecItem(LSPDr,IVCashr,IVCashrw,true,vatrates);
        if (res!=noErr) then begin
          goto LPrintItemRows;
        end;
    end;
  end;
LPrintItemRows:;  
  IVCash_PrintItemRows = res;
  return;
end;

function Integer LoadVATRates(record LSerialPortDeviceVc LSPDr,vector val vatrates)
begin
  Integer res,cnt,i;
  string 255 path,tstr,stat;
  area request;
  JSON js;
  val rate;
  Longint pos;


  path = "/json/fp550/Cmdline";
  AddTextToArea("{""cmd"":""83"",""parameter"":""""}",request);

  res = ExecAPSARequest(LSPDr,path,request,js);
  if (res==0) then begin
    stat = JSONGet(js,"CmdlineResult");
    GetNextSubString(stat,pos,",",tstr);
    GetNextSubString(stat,pos,",",tstr);
    GetNextSubString(stat,pos,",",tstr);
    GetNextSubString(stat,pos,",",tstr);

    GetNextSubString(stat,pos,",",tstr);

    for (i=0;i<6;i=i+1) begin
      GetNextSubString(stat,pos,",",tstr);
      rate = StringToVal(tstr,M4Val);
      vatrates[i] = rate;
    end;
  end;
  
  
  LoadVATRates = res;
  return;
end;


global
updating procedure IVCashVc_PrintAPSAReceipt(var record IVCashVc IVCashr,record LSerialPortDeviceVc LSPDr)
begin
  record IVCashVc oldIVCashr;
  Integer noErr,res;
  record LocalMachineBlock LMb;
  record MachineCashVc MCr;
  vector val vatrates;
  
  noErr = 0;
  //res = APSACheckStatus;
  if (res==noErr) then begin
    noErr = 0;
    BlockLoad(LMb);

  /*  
    res = LoadAPSAVATRates(vatrates);
    if (res!=noErr) then begin
      goto LIVCashVc_PrintAPSAReceipt;
    end;
  */
    res = LoadVATRates(LSPDr,vatrates);  
    if (res!=noErr) then begin
      goto LIVCashVc_PrintAPSAReceipt;
    end;
    res = BeginFiscalReceipt(LSPDr);
    if (res!=noErr) then begin
      goto LIVCashVc_PrintAPSAReceipt;
    end;
    res = IVCash_PrintItemRows(LSPDr,IVCashr,vatrates);
    if (res!=noErr) then begin
      goto LIVCashVc_PrintAPSAReceipt;
    end;
    res = SendAPSAPayments(LSPDr,IVCashr);
    if (res!=noErr) then begin
      goto LIVCashVc_PrintAPSAReceipt;
    end;
    
    res = IVCash_EndFiscalReceipt(LSPDr);
    if (res!=noErr) then begin
      goto LIVCashVc_PrintAPSAReceipt;
    end;
/* This is needed only when closing computer
    res = APSAClose(LSPDr);
    if (res!=noErr) then begin
      goto LIVCashVc_PrintAPSAReceipt;
    end;
*/
    if (IVCashr.Prntdf!=0) then begin
      RecordCopy(oldIVCashr,IVCashr);
      IVCashr.Prntdf = 1;
      RecordUpdate(oldIVCashr,IVCashr,false);
    end;    
LIVCashVc_PrintAPSAReceipt:;
    if (res!=0) then begin
      MessageBox(res,"");
    end;
  end;
  return;
end;

global
function Integer APSA_PrintZReport(record LSerialPortDeviceVc LSPDr)
begin
  Integer res;
  
  res = SendAPSASimpleCommand(LSPDr,kAPSACommandZReport);

  APSA_PrintZReport = res;
  return;
end;

global
function Integer APSA_PrintXReport(record LSerialPortDeviceVc LSPDr)
begin
  Integer res;
  
  res = SendAPSASimpleCommand(LSPDr,kAPSACommandXReport);

  APSA_PrintXReport = res;
  return;
end;

function Integer MoneyOutCurr(record LSerialPortDeviceVc LSPDr,record CashVc Cashr)
begin
  Integer res,msglen,noErr;
  string 255 path;
  area request;
  JSON js;
    
  path = "/json/fp550/Cmdline";
  AddTextToArea("{""cmd"":""70"",""parameter"":""" & ValToString(-Cashr.Total,M4UVal,"",".",0) & """}",request);

  res = ExecAPSARequest(LSPDr,path,request,js);

  MoneyOutCurr = res;
  return;
end;

function Integer MoneyInCurr(record LSerialPortDeviceVc LSPDr,record CashVc Cashr)
begin
  Integer res,msglen,noErr;
  string 255 path;
  area request;
  JSON js;
    
  path = "/json/fp550/Cmdline";
  AddTextToArea("{""cmd"":""70"",""parameter"":""" & ValToString(Cashr.Total,M4UVal,"",".",0) & """}",request);

  res = ExecAPSARequest(LSPDr,path,request,js);

  MoneyInCurr = res;
  return;
end;

global
function Integer APSA_CashEvent(var record CashVc Cashr,record LSerialPortDeviceVc LSPDr)
begin
  Integer noErr,res;
  
//  res = FB05Open(LSPDr);
//  if (res==noErr) then begin    
    switch (Cashr.Event) begin
      case 0:
        res = MoneyOutCurr(LSPDr,Cashr);
      case 1:
        res = MoneyInCurr(LSPDr,Cashr);
    end;
LPrintFB05CashEvent:;
//    FB05Close;
    if (res!=0) then begin
      MessageBox(res,"");
    end;
//  end;

  APSA_CashEvent = res;
  return;
end;

global
procedure SetPOSWindowDisplayModeFiscalPrinterAPSA(record LSerialPortDeviceVc LSPDr,string dispstr,string disp2str)
begin
  Integer res,msglen,noErr;
  string 255 path;
  area request;
  JSON js;
    
  //clear 

  path = "/json/fp550/Cmdline";
 
  AddTextToArea("{""cmd"":""33"",""parameter"":""""}",request);
  res = ExecAPSARequest(LSPDr,path,request,js);

  if (nonblank(dispstr)) then begin
    SetAreaZeroSize(request);
    AddTextToArea("{""cmd"":""47"",""parameter"":""" & ConvertStringToCodePage("CP1257",dispstr) & """}",request);
    res = ExecAPSARequest(LSPDr,path,request,js);
  end;

  if (nonblank(disp2str)) then begin
    SetAreaZeroSize(request);
    AddTextToArea("{""cmd"":""35"",""parameter"":""" & ConvertStringToCodePage("CP1257",disp2str) & """}",request);
    res = ExecAPSARequest(LSPDr,path,request,js);
  end;

  return;
end;

global
procedure APSAManageConnection(Boolean loginf)
begin
  record LocalMachineBlock LMb;
  record LSerialPortDeviceVc LSPDr;
  Integer res;
    
  BlockLoad(LMb);  
  if (FindLocalSerialPortDevice(kSerialPortDeviceClassFiscalPrinter,-1,LMb.LocalMachineCode,LSPDr)==false) then begin  
    goto LAPSACloseConnection;
  end;
  switch (LSPDr.Printer) begin
    case 200:
      if (loginf) then begin
        res = APSAOpen(LSPDr);
        if (res!=0) then begin
          MessageBox(22101,"");
        end;
      end else begin
        res = APSAClose(LSPDr);
      end;
  end;
LAPSACloseConnection:;  
  return;
end;
