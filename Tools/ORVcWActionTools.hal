external inner function Boolean GetFirstItem(var string,var record INVc);
external inner procedure GetVATRowFromBlock(string,record VATCodeBlock,var row VATCodeBlock);
external inner procedure IV2Sumup3(record ORVc,record SMVc,Integer,record SMVc,record SMVc,Boolean,Boolean,Integer,var val,var val,var val,var val,var val,var val,var string);
external inner procedure SetupVATBase(var record SMVc,var Integer);
external inner function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external inner function val FindVAT(string, val, integer,integer);
external inner procedure ORDchsum(var record ORVc,Integer);
external inner procedure ORSumup(var record ORVc,Boolean);
external function Boolean IsRvrsVATPayDeal(string);

procedure AddToRvrsVATCodes(string rvrsvatcode,array string arvrsvatcodes)
begin
  Integer i;
  
  for (i=0;i<arvrsvatcodes.length;i=i+1) begin
    if (arvrsvatcodes[i]==rvrsvatcode) then begin
      goto LAddToRvrsVATCodes;
    end;
  end;
  arvrsvatcodes[arvrsvatcodes.length] = rvrsvatcode;
LAddToRvrsVATCodes:;  
  return;
end;

procedure UpdateOrderPrice(var record ORVc ORr,var row ORVc ORrw)
begin
  record AccBlock Accb;

  if (ORr.InclVAT == 0 or nonblank(ORrw.RvrsVATCode)) then begin
    ORrw.Price = ORrw.PriceExclVAT;
  end else begin
    ORrw.Price = ORrw.PriceExclVAT + FindVAT(ORrw.VATCode,ORrw.PriceExclVAT,0,ORr.NoTAXonVAT);
  end;
  if (ORrw.RvrsVATCode=="PVM25") then begin
    ORr.ShipDeal = "PVM25";
  end;

  if (nonblank(ORrw.RvrsVATCode)) then begin
    ORrw.VATCode = ORrw.RvrsVATCode;
  end else begin
    BlockLoad(Accb);
    ORrw.VATCode = Accb.VATCodeDom;
  end;

  return;
end;

procedure IVPasteRvrsVATCode2(var record ORVc aORr,Boolean clearf)
begin
  record ORVc ORr;
  row ORVc ORrw;
  Integer i,j,rwcnt;
  record INVc INr;
  record VATCodeBlock VATCb;
  row VATCodeBlock VATCbrw;
  array string 255 arvrsvatcodes;
  vector Boolean vrvrsvatcodesf;
  vector val vrvrsvatcodesv;
  record SMVc VATr;
  record SMVc SMTax2r;
  record SMVc RvrsVATr;
  row SMVc RvrsVATrw;
  Integer vatcnt;
  Boolean testf;
  val s0,s1,s2,s3,s4,rvrsvat,TotGP,t;
  string 255 cashpaymode;
  record CUVc CUr;
  record ITVc ITr;
  
  CUr.Code = aORr.CustCode;
  ReadFirstMain(CUr,1,true);
  //if (CUr.RvrsVAT==0 or CUr.CustType==1 or forcef) and (aORr.InclVAT==0) then begin
    RecordCopy(ORr,aORr);
    rwcnt = MatRowCnt(ORr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(ORr,i,ORrw);
      if (ORrw.stp==kInvoiceRowTypeNormal or ORrw.stp==kInvoiceRowTypeCorrection) then begin
        if (nonblank(ORrw.ArtCode)) then begin
          if (CUr.CustType==1 or CUr.RvrsVAT==0 or clearf) then begin
            ORrw.RvrsVATCode = "";
            UpdateOrderPrice(ORr,ORrw);
            MatRowPut(ORr,i,ORrw);
            ORDchsum(ORr,i);
          end else begin          
            testf = GetFirstItem(ORrw.ArtCode,INr);
            if (testf==false) then begin
              testf = ReadFirstItem(ORrw.ArtCode,INr,true,false);
            end;
            if (nonblank(INr.RvrsVATCode)) then begin
              ORrw.RvrsVATCode = INr.RvrsVATCode;
              UpdateOrderPrice(ORr,ORrw);
              MatRowPut(ORr,i,ORrw);
              IVDchsum(ORr,i);
              AddToRvrsVATCodes(ORrw.RvrsVATCode,arvrsvatcodes);
            end else begin
              ITr.Code = INr.Group;
              if (ReadFirstMain(ITr,1,true)) then begin
                if (nonblank(ITr.RvrsVATCode)) then begin
                  ORrw.RvrsVATCode = ITr.RvrsVATCode;
                  UpdateOrderPrice(ORr,ORrw);
                  MatRowPut(ORr,i,ORrw);
                  IVDchsum(ORr,i);
                  AddToRvrsVATCodes(ORrw.RvrsVATCode,arvrsvatcodes);
                end;
              end;
            end;
          end;
        end;
      end;
    end;
    ORSumup(ORr,true);
    if (arvrsvatcodes.length>0) then begin
      if (CUr.RvrsVAT!=0) then begin
        RecordCopy(aORr,ORr);
      end else begin
        BlockLoad(VATCb);
        SetupVATBase(VATr,vatcnt);
        SetupVATBase(RvrsVATr,vatcnt);
        for (i=0;i<arvrsvatcodes.length;i=i+1) begin
          for (j=0;j<vatcnt;j=j+1) begin
            MatRowGet(RvrsVATr,j,RvrsVATrw);
            if (arvrsvatcodes[i]==RvrsVATrw.VATCode) then begin
              vrvrsvatcodesv[arvrsvatcodes[i]] = RvrsVATrw.DebVal;
            end;
          end;
        end;
        for (i=0;i<arvrsvatcodes.length;i=i+1) begin
          testf = true;
          GetVATRowFromBlock(arvrsvatcodes[i],VATCb,VATCbrw);
          if (VATCbrw.MinBaseAmount>0) then begin
            if (vrvrsvatcodesv[arvrsvatcodes[i]]<=VATCbrw.MinBaseAmount) then begin testf = false; end;
          end;
          if (VATCbrw.MaxBaseAmount>0) then begin
            if (vrvrsvatcodesv[arvrsvatcodes[i]]>VATCbrw.MaxBaseAmount) then begin testf = false; end;
          end;
          vrvrsvatcodesf[arvrsvatcodes[i]] = testf;
        end;
        for (i=0;i<rwcnt;i=i+1) begin
          MatRowGet(ORr,i,ORrw);
          if (vrvrsvatcodesf[ORrw.RvrsVATCode]==false) then begin
            ORrw.RvrsVATCode = "";
            MatRowPut(ORr,i,ORrw);
          end;
        end;
        RecordCopy(aORr,ORr);        
      end;
    end else begin
      RecordCopy(aORr,ORr);
    end;
  //end;
  
  return;
end;

global
procedure UpdateiDealRvrsVATCodesOrder(var record ORVc ORr)
begin
  
  if (IsRvrsVATPayDeal(ORr.PayDeal)) then begin
    IVPasteRvrsVATCode2(ORr,false);
  end else begin
    IVPasteRvrsVATCode2(ORr,true);
  end;

  return;
end;