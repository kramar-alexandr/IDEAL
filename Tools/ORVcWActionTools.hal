external inner function Boolean GetFirstItem(var string,var record INVc);
external inner procedure GetVATRowFromBlock(string,record VATCodeBlock,var row VATCodeBlock);
external inner procedure SetupVATBase(var record SMVc,var Integer);
external inner function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external inner function val FindVAT(string, val, integer,integer);
external inner procedure ORDchsum(var record ORVc,Integer);
external inner procedure ORSumup(var record ORVc);
external function Boolean IsRvrsVATPayDeal(string);
external inner function Boolean GetItemPriceDiscount3(string,val,var record INVc,string,val,val,val,val,val,string,string,string,string,
                                                var val,var string,var val,var string,var val,var string,Integer,var Boolean,Date,Time,
                                                string,Boolean,var Boolean,string,var string,var val,string,string,var string);
external inner function Boolean ORDchrsum(var record ORVc,Integer);

procedure AddToRvrsVATCodes(string rvrsvatcode,array string arvrsvatcodes)
begin
  Integer i;
  
  for (i=0;i<arvrsvatcodes.length;i=i+1) begin
    if (arvrsvatcodes[i]==rvrsvatcode) then begin
      goto LAddToRvrsVATCodes;
    end;
  end;
  arvrsvatcodes[arvrsvatcodes.length] = rvrsvatcode;
LAddToRvrsVATCodes:;  
  return;
end;

procedure UpdateOrderPrice(var record ORVc ORr,var row ORVc ORrw,Boolean hadrevf,Boolean updatepricef)
begin
  record AccBlock Accb;
  val t,s,price,vreb,baseprice,temp,tax2prc,tax2reb;
  string 255 curitemname,salesacc,vatcode,tstr,tax2code,location,taxtemplatecode;
  Boolean calcprice;
  record INVc INr;
  Date orddate;
  Boolean dummyf;
  Time blankt;

logtext(0,"UpdateOrderPrice " & updatepricef);
  if (updatepricef) then begin
    if (GetItemPriceDiscount3(ORrw.ArtCode,ORrw.Quant,INr,ORr.CurncyCode,
                              ORr.FrRate,ORr.ToRateB1,ORr.ToRateB2,ORr.BaseRate1,ORr.BaseRate2,
                              ORr.LangCode,ORr.CustCat,ORr.PriceList,ORr.RebCode,
                              price,curitemname,vreb,vatcode,baseprice,salesacc,
                              ORr.ExportFlag,calcprice,orddate,blankt,ORr.CustCode,true,dummyf,ORr.PayDeal,tax2code,tax2prc,ORr.Region,location,taxtemplatecode)) then begin

      ORrw.Price = price;

      if (ORr.InclVAT == 2 and blank(ORrw.RvrsVATCode)) then begin
        ORrw.Price = ORrw.Price + FindVAT(ORrw.VATCode,ORrw.Price,0,ORr.NoTAXonVAT);
      end;
    end;  
  end;

  if (ORrw.RvrsVATCode=="PVM25") then begin
    ORr.ShipDeal = "PVM25";
  end;

  if (nonblank(ORrw.RvrsVATCode)) then begin
    ORrw.VATCode = ORrw.RvrsVATCode;
  end else begin
    if (hadrevf) then begin
      BlockLoad(Accb);
      ORrw.VATCode = Accb.VATCodeDom;
    end;
  end;

  return;
end;

function string 255 GetReverseVATCode(string item)
begin
  Boolean testf;
  string 255 res;
  record INVc INr;
  record ITVc ITr;
  
  testf = GetFirstItem(item,INr);
  if (testf==false) then begin
    testf = ReadFirstItem(item,INr,true,false);
  end;
  res = INr.RvrsVATCode;
  if (blank(res)) then begin
    ITr.Code = INr.Group;
    if (ReadFirstMain(ITr,1,true)) then begin
      res = ITr.RvrsVATCode;
    end;
  end;

  GetReverseVATCode = res;
  return;
end;

procedure UpdateSingleOrderRow(var record ORVc ORr,record CUVc CUr,Integer i,Boolean clearf,var array string arvrsvatcodes,Boolean updatepricef)
begin
  row ORVc ORrw;
  string 255 vatcode;
  
  MatRowGet(ORr,i,ORrw);
  if (ORrw.stp==kInvoiceRowTypeNormal or ORrw.stp==kInvoiceRowTypeCorrection) then begin
    if (nonblank(ORrw.ArtCode)) then begin
      if (CUr.CustType==1 or CUr.RvrsVAT==0 or clearf) then begin
        ORrw.RvrsVATCode = "";
        UpdateOrderPrice(ORr,ORrw,nonblank(GetReverseVATCode(ORrw.ArtCode)),updatepricef);
        MatRowPut(ORr,i,ORrw);
        if (ORDchrsum(ORr,i)) then begin
          ORDchsum(ORr,i);
        end;
      end else begin          
        vatcode = GetReverseVATCode(ORrw.ArtCode);
        if (nonblank(vatcode)) then begin
          ORrw.RvrsVATCode = vatcode;
          UpdateOrderPrice(ORr,ORrw,true,updatepricef);
          MatRowPut(ORr,i,ORrw);
          if (ORDchrsum(ORr,i)) then begin
            ORDchsum(ORr,i);
          end;
          AddToRvrsVATCodes(ORrw.RvrsVATCode,arvrsvatcodes);
        end;
      end;
    end;
  end;


  return;
end;

procedure IVPasteRvrsVATCode2(var record ORVc aORr,Boolean clearf,Integer rw,Boolean updatepricef)
begin
  record ORVc ORr;
  row ORVc ORrw,OR2rw;
  Integer i,j,rwcnt;
  record INVc INr;
  record VATCodeBlock VATCb;
  row VATCodeBlock VATCbrw;
  array string 255 arvrsvatcodes;
  vector Boolean vrvrsvatcodesf;
  vector val vrvrsvatcodesv;
  record SMVc VATr;
  record SMVc SMTax2r;
  record SMVc RvrsVATr;
  row SMVc RvrsVATrw;
  Integer vatcnt;
  Boolean testf;
  val s0,s1,s2,s3,s4,rvrsvat,TotGP,t;
  string 255 cashpaymode;
  record CUVc CUr;
  record ITVc ITr;
  string 255 vatcode;
  
  CUr.Code = aORr.CustCode;
  ReadFirstMain(CUr,1,true);
  //if (CUr.RvrsVAT==0 or CUr.CustType==1 or forcef) and (aORr.InclVAT==0) then begin
    RecordCopy(ORr,aORr);
    rwcnt = MatRowCnt(ORr);
    if (rw>-1) then begin
      UpdateSingleOrderRow(ORr,CUr,rw,clearf,arvrsvatcodes,updatepricef);
    end else begin
      for (i=0;i<rwcnt;i=i+1) begin
        UpdateSingleOrderRow(ORr,CUr,i,clearf,arvrsvatcodes,updatepricef);
      end;
    end;
    ORSumup(ORr);
    if (arvrsvatcodes.length>0) then begin
      if (CUr.RvrsVAT!=0) then begin
        RecordCopy(aORr,ORr);
      end else begin
        BlockLoad(VATCb);
        SetupVATBase(VATr,vatcnt);
        SetupVATBase(RvrsVATr,vatcnt);
        for (i=0;i<arvrsvatcodes.length;i=i+1) begin
          for (j=0;j<vatcnt;j=j+1) begin
            MatRowGet(RvrsVATr,j,RvrsVATrw);
            if (arvrsvatcodes[i]==RvrsVATrw.VATCode) then begin
              vrvrsvatcodesv[arvrsvatcodes[i]] = RvrsVATrw.DebVal;
            end;
          end;
        end;
        for (i=0;i<arvrsvatcodes.length;i=i+1) begin
          testf = true;
          GetVATRowFromBlock(arvrsvatcodes[i],VATCb,VATCbrw);
          if (VATCbrw.MinBaseAmount>0) then begin
            if (vrvrsvatcodesv[arvrsvatcodes[i]]<=VATCbrw.MinBaseAmount) then begin testf = false; end;
          end;
          if (VATCbrw.MaxBaseAmount>0) then begin
            if (vrvrsvatcodesv[arvrsvatcodes[i]]>VATCbrw.MaxBaseAmount) then begin testf = false; end;
          end;
          vrvrsvatcodesf[arvrsvatcodes[i]] = testf;
        end;
        for (i=0;i<rwcnt;i=i+1) begin
          MatRowGet(ORr,i,ORrw);
          if (vrvrsvatcodesf[ORrw.RvrsVATCode]==false) then begin
            ORrw.RvrsVATCode = "";
            MatRowPut(ORr,i,ORrw);
          end;
        end;
        RecordCopy(aORr,ORr);        
      end;
    end else begin
      RecordCopy(aORr,ORr);
    end;
  //end;
  
  return;
end;

global
procedure UpdateiDealRvrsVATCodesOrder(var record ORVc ORr,Integer rw,Boolean updatepricef)
begin
  
  if (IsRvrsVATPayDeal(ORr.PayDeal)) then begin
    IVPasteRvrsVATCode2(ORr,false,rw,updatepricef);
  end else begin
    IVPasteRvrsVATCode2(ORr,true,rw,updatepricef);
  end;

  return;
end;